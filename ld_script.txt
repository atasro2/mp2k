SECTIONS {
    . = 0x2000000;
    ewram (NOLOAD) :
    ALIGN(4)
    {
        *.o(ewram_data);
    }
    . = 0x3000000;
    iwram (NOLOAD) :
    ALIGN(4)
    {
        *.o(.bss);
        *.o(COMMON);
        *.o(iwram_data);
        INCLUDE "sym_iwram.ld"
    }
    . = 0x8000000;
    .text :
    ALIGN(4)
    {
        asm/crt0.o(.text);
        src/m4a.o(.text);
        asm/m4a.o(.text);
        asm/*.o(.text);
        src/*.o(.text);
        *libgcc.a:*.o(.text*);
        *libc.a:*.o(.text*);
    } =0
    .rodata :
    ALIGN(4)
    {
        asm/crt0.o(.rodata);
        data/data.o(.data);
        data/SndMonDat.o(.rodata);
        data/SoundDat.o(.rodata);
    } =0
    
    song_data :
    ALIGN(4)
    {
        sound/songs/*.o(.rodata);
        sound/songs/midi/*.o(.rodata);
    } =0
    
    .rodata.end :
    ALIGN(4)
    {
        data/SndMonDat.o(.rodata.end);
    } =0
    /* DWARF debug sections.
    Symbols in the DWARF debugging sections are relative to the beginning
    of the section so we begin them at 0.  */

    /* DWARF 1 */
    .debug          0 : { *(.debug) }
    .line           0 : { *(.line) }

    /* GNU DWARF 1 extensions */
    .debug_srcinfo  0 : { *(.debug_srcinfo) }
    .debug_sfnames  0 : { *(.debug_sfnames) }

    /* DWARF 1.1 and DWARF 2 */
    .debug_aranges  0 : { *(.debug_aranges) }
    .debug_pubnames 0 : { *(.debug_pubnames) }

    /* DWARF 2 */
    .debug_info     0 : { *(.debug_info .gnu.linkonce.wi.*) }
    .debug_abbrev   0 : { *(.debug_abbrev) }
    .debug_line     0 : { *(.debug_line) }
    .debug_frame    0 : { *(.debug_frame) }
    .debug_str      0 : { *(.debug_str) }
    .debug_loc      0 : { *(.debug_loc) }
    .debug_macinfo  0 : { *(.debug_macinfo) }

    /* Discard everything not specifically mentioned above. */
    
	/DISCARD/ :
    {
        *(*);
    }
	
}
